buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'org.ajoberstar:grgit:1.4.0'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
}

dependencies {
    compile 'joda-time:joda-time:2.7'
    compile 'com.larswerkman:HoloColorPicker:1.5'
    compile 'com.squareup.okhttp3:okhttp:3.4.2'
    compile 'com.squareup.moshi:moshi:1.3.1'
}

android {

    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        versionName '1.9.3' + getVersionNameSuffix()
        versionCode getVersionCodeInteger()
        minSdkVersion 15
        targetSdkVersion 22

        testApplicationId "com.plusonelabs.tasks.tests"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    if (project.hasProperty('calendarStoreFile')) {
        signingConfigs {
            releaseConfig {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        buildTypes.release.signingConfig signingConfigs.releaseConfig
    }
    // See https://www.timroes.de/2013/09/22/handling-signing-configs-with-gradle/
    if (project.hasProperty("calendar-widget.signing")
            && new File(project.property("calendar-widget.signing").toString() + ".gradle").exists()) {
        apply from: project.property("calendar-widget.signing") + ".gradle";
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

String getVersionNameSuffix() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open(dir: '../..')
        def versionNameSuffix = "-${git.head().abbreviatedId}"
        git.close()
        return versionNameSuffix
    } catch (ignored) {
        return ""
    }
}

def getVersionCodeInteger() {
    try {
        def git = org.ajoberstar.grgit.Grgit.open(dir: '../..')
        def versionCode = git.log().size()
        git.close()
        return versionCode
    } catch (ignored) {
        return 1
    }

}
